#!/usr/bin/env perl
use strict;
use warnings;
use feature 'say';
use FindBin qw< $Script >;
use Getopt::Long;

my ($from, $to, $offset, $length);
GetOptions(
    'from=s'    => \$from,
    'to=s'      => \$to,
    'offset=s'  => \$offset,
    'length=s'  => \$length,
);

unless ($from and $to and defined $offset and defined $length) {
    warn <<USAGE;
usage: $Script --from refA --to refB --offset N --length N [input BED files]

Liftsover coordinates in a BED file from one reference to another
with a given offset.  Reads input from stdin or filenames listed
on the command line, outputs a BED file to stdout.
USAGE
    exit 1;
}

while (<>) {
    chomp;
    my @F = split /\t/;

    say, next if /^#/;
    say, next if $F[0] ne $from;

    $F[0] = $to;
    $_ -= $offset for @F[1,2];
    say join "\t", @F unless $F[1] < 0 or $F[2] > $length;
}
